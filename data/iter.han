
mod builtin;
mod list;

proc empty(caller) {
    become caller(this.empty_impl)
}

proc empty_impl(caller, @next) {
    become caller(@end, @eos)
}

proc once(caller, item) {
    become caller(this.once_impl&(item))
}

proc once_impl(item, caller, @next) {
    let (caller, @next) = resp caller(@yield, item);
    become caller(@end, @eos)
}

proc proxy(caller) {
    let (caller) = req caller(@iter);
    let (caller) = caller();
    become this.proxy(caller)
}

proc symbol_chars_inner(symbol, offset, caller, @next) {
    let (len) = this.builtin.symbol_len(*symbol);
    let (are_eq) = this.builtin.eq(*offset, len);
    if are_eq {
        become caller(@end, @eos)
    } else {
        let (char) = this.builtin.symbol_char_at(*symbol, *offset);
        let (offset) = this.builtin.add(offset, 1);
        let () = resp caller(@yield, char);
        become this.symbol_chars_inner(symbol, offset)
    }
}

proc symbol_chars(caller, symbol) {
    become caller(this.symbol_chars_inner&(symbol, 0))
}

proc map(caller, @next) {
    let (caller) = req caller(@next);
    match {
        (@eos) => {
            let () = caller(@eos);
        }
        (@yield, item) => {
            let (caller) = req caller(@fn, item);
            match {
                (@ok, applied) => {
                    let () = caller(@yield, applied);
                    become this.map()
                }
            }
        }
    }
}


proc multi_map(caller, @next) {
    let (caller) = req caller(@next);
    match {
        (@eos) => {
            let () = caller(@eos);
        }
        (@start) => {
            become this.multi_map_loop(caller)
        }
    }
}

proc multi_map_loop(caller) {
    let (caller) = req caller(@next);
    match {
        (@yield, item) => {
            let (caller) = req caller(@reducer, @push, item);
            become this.multi_map_loop(caller)
        }
        (@end) => {
            let (caller, @ok, reduced) = req caller(@reducer, @finish);
            let (caller) = caller(@yield, reduced);
            become this.multi_map(caller)
        }
    }
}


proc do_bind_iter(inner, symbol, iter, caller) {
    let (inner) = inner();
    match {
        (@req, name) =>  {
            let (are_eq) = this.builtin.eq(*name, *symbol);
            if are_eq {
                let (iter) = iter();
                become this.do_bind_iter(inner, symbol, iter, caller)
            } else {
                let (caller) = req caller(name);
                become this.do_bind_iter(inner, symbol, iter, caller)
            }
        }
        (tag) => {
            let (caller) = caller(tag);
            become this.do_bind_iter(inner, symbol, iter, caller)
            
        }
    }
}

proc alternate(caller) {
    let (l1) = this.list.new();
    let (l2) = this.list.new();
    become caller(this.alternate_impl&(l1, l2))
}

proc alternate_impl(aq, bq, caller, @get) {
    let (caller) = req caller(@next);
    match {
        (@eos) => {
            become caller(@end, aq, bq)
        }
        (@yield, item) => {
            let (@resp, aq) = aq(@push_back, item);
            become this.alternate_impl(bq, aq, caller, @get)
        }
    }
}

proc until_ok(next, up, down) {
    let (down) = down();
    match {
        (@ok) => {
            become next(up, down)
        }
        (tag) => {
            let (up) = up(tag);
            become this.until_ok(next, up, down)
        }
    }
}

proc cons(caller, head, tail) {
    become caller(this.cons_impl&(head, tail))
}

proc cons_impl(head, tail, caller, @next) {
    let (caller) = caller(@yield, head);
    become tail(caller)
}

proc prime_one(caller, head) {
    become caller(this.prime_one_impl&(head))
}

proc prime_one_impl(head, caller, @next) {
    let () = resp caller(@yield, head);
    become this.prime_one_proxy()
}

proc prime_one_proxy(caller, @next) {
    let (caller) = req caller(@next);
    let (caller) = resp caller();
    become this.prime_one_proxy(caller)
}
proc merge_sorted(caller, @next) {
    let (caller) = req caller(@a, @next);
    match {
        (@eos) => {
            become this.merge_sorted_proxy_to_b(caller, @next)
        }
        (@yield, a_item) => {
            become this.merge_sorted_with_a(a_item, caller, @next)
        }
    }
}

proc merge_sorted_proxy_to_a(caller) {
    let (caller) = req caller(@a);
    let () = resp caller();
    become this.merge_sorted_proxy_to_a()
}

proc merge_sorted_proxy_to_b(caller) {
    let (caller) = req caller(@b);
    let () = resp caller();
    become this.merge_sorted_proxy_to_b()
}

proc merge_sorted_with_a(a_item, caller, @next) {
    let (caller) = req caller(@b, @next);
    match {
        (@eos) => {
            let () = resp caller(@yield, a_item);
            become this.merge_sorted_proxy_to_a()
        }
        (@yield, b_item) => {
            become this.merge_sorted_with_both(a_item, b_item, caller, @next)
        }
    }
}

proc merge_sorted_with_b(b_item, caller, @next) {
    let (caller) = req caller(@a, @next);
    match {
        (@eos) => {
            let () = resp caller(@yield, b_item);
            become this.merge_sorted_proxy_to_b()
        }
        (@yield, a_item) => {
            become this.merge_sorted_with_both(a_item, b_item, caller, @next)
        }
    }
}

proc merge_sorted_with_both(a_item, b_item, caller, @next) {
    let (lt) = this.builtin.lt(*a_item, *b_item);
    if lt {
        let () = resp caller(@yield, a_item);
        become this.merge_sorted_with_b(b_item)
    } else {
        let () = resp caller(@yield, b_item);
        become this.merge_sorted_with_a(a_item)
    }
}

proc mergesort(caller, @next) {
    let (caller) = this.builtin.fwd_all(caller, this.none_one_many);
    match {
        (@none) => {
            become caller(@end, @eos)
        }
        (@one, item) => {
            let (caller, @next) = resp caller(@yield, item);
            become caller(@end, @eos)
        }
        (@many, item1, item2) => {
            let (alternate) = this.alternate();
            let (caller, aq, bq) = this.builtin.fwd_all(caller, alternate, @get);

            let (@resp, aq) = aq(@push_front, item1);
            let (@end, aq) = aq(@into_iter);
            let (@resp, bq) = bq(@push_front, item2);
            let (@end, bq) = bq(@into_iter);

            let (aq) = this.builtin.compose(aq, this.mergesort);
            let (bq) = this.builtin.compose(bq, this.mergesort);

            let (c) = this.builtin.mk_pair(aq, bq);
            
            let (mq) = this.builtin.compose(c, this.merge_sorted);
            become mq(caller, @next)
        }
    }
}

proc peeking(caller) {
    become caller(this.peeking_start)
}

proc peeking_start(caller) {
    let (caller) = req caller(@next);
    match {
        (@yield, val) => {
            become this.peeking_impl(@some, val, caller)
        }
        (@eos) => {
            become this.peeking_impl(@none, caller)
        }
    }
}

// nextval: maybe
// caller
// request
proc peeking_impl() {
    match {
        (@none, caller) => {
            match {
                (@peek) => {
                    let () = resp caller(@none);
                    become this.peeking_impl(@none)
                }
                (@next) => {
                    become caller(@end, @eos)
                }
            }
        }
        (@some, val, caller) => {
            match {
                (@peek) => {
                    let () = resp caller(@some, *val);
                    become this.peeking_impl(@some, val)
                }
                (@next) => {
                    let (caller) = req caller(@next);
                    match {
                        (@yield, newval) => {
                            let () = resp caller(@yield, val);
                            become this.peeking_impl(@some, newval)
                        }
                        (@eos) => {
                            let () = resp caller(@yield, val);
                            become this.peeking_impl(@none)
                        }
                    }
                }
            }
        }
    }
}

proc split(caller, @next) {
    let () = resp caller(@start);
    become this.split_loop()
}

proc split_loop(caller, @req, @next) {
    let (caller) = req caller(@next);
    match {
        (@eos) => {
            let (caller, @next) = resp caller(@eos);
            become caller(@end, @eos)
        }
        (@delimiter) => {
            let (caller, @next) = resp caller(@eos);
            let () = resp caller(@start);
            become this.split_loop()
        }        
        (@non_delimiter) => {
            let (caller) = resp caller(@start);  // Start of char.

            let () = this.split_char_proxy(caller);
            become this.split_loop()
        }
    }
}

proc split_char_proxy(next, caller) {
    match {
        (@req, @req) => {
            let (caller) = req caller(@req);
            let () = resp caller();
            become this.split_char_proxy(next)
        }
        (@req, @next) => {
            become next(caller, @req, @next)
        }
    }
}

// proc mergesort_both(alternate, ma, mb, caller, @req) {
//     match {
//         (@a, @next) => {
//             let (ma) = ma(@next);
//             match {
//                 (@resp) => {
//                     let () = resp caller();
//                     become mergesort_both(alternate, ma, mb)
//                 }
//                 (@req, @next) => {
//                     let (caller, alternate) = this.builtin.fwd_req(caller, alternate, @next);
//                     let (ma) = ma();
//                     become mergesort_both(alternate, ma, mb)
//                 }
//                 (@req, other) => {
//                     let (caller) = req caller(other);
//                     let (ma) = ma();
//                     become mergesort_both(alternate, ma, mb)
//                 }
//             }
//         }
//         (@b, @next) => {
//             let (mb) = mb(@next);
//             match {
//                 (@resp) => {
//                     let () = resp caller();
//                     become mergesort_both(alternate, ma, mb)
//                 }
//                 (@req, @next) => {
//                     let (caller, alternate) = this.builtin.fwd_req(caller, alternate, @next);
//                     let (mb) = mb();
//                     become mergesort_both(alternate, ma, mb)
//                 }
//                 (@req, other) => {
//                     let (caller) = req caller(other);
//                     let (mb) = mb();
//                     become mergesort_both(alternate, ma, mb)
//                 }
//             }
//         }
//     }
// }

//proc nest_pair(inner, caller, @req) {
//    let (inner) = inner();
//    match {
//        (@req, @a) => {
//            let (caller) = req caller(@pair, @req, @a);
//            become nest_pair(inner, caller)
//        }
//        (@req, @b) => {
//            let (caller) = req caller(@pair, @req, @b);
//            become nest_pair(inner, caller)
//        }
//        (@req, other) => {
//            let (caller) = req caller(other);
//            become nest_pair(inner, caller)
//        }
//        (other) => {
//            let (caller) = caller(other);
//            become nest_pair(inner, caller)
//        }
//    }
//}

proc bind_pair(caller, inner, a, b) {
    become caller(this.bind_pair_impl&(inner, a, b))
}

proc bind_pair_impl(inner, a, b, caller) {
    let (inner) = inner();
    match {
        (@req, @a) => {
            let (caller, a) = this.builtin.fwd_req(caller, a);
            become this.bind_pair_impl(inner, a, b, caller)
        }
        (@req, @b) => {
            let (caller, b) = this.builtin.fwd_req(caller, b);
            become this.bind_pair_impl(inner, a, b, caller)
        }
        (@resp) => {
            let (caller) = resp caller();
            become this.bind_pair_impl(inner, a, b, caller)
        }
        // Not sure if I need these...
        // (@req, other) => {
        //     let (caller) = req caller(other);
        //     become bind_pair_impl(inner, a, b, caller)
        // }
        // (other) => {
        //     let (caller) = caller(other);
        //     become nest_pair(inner, caller)
        // }
    }
}

proc forward_iter_to_sink(caller, iter, sink) {
    let (iter) = iter(@next);
    match {
        (@eos) => {
            let () = sink(@finish);
            become caller()
        }
        (@yield, item) => {
            let (sink) = sink(@push, item);
            become this.forward_iter_to_sink(caller, iter, sink)
        }
    }
}

proc none_one_many(caller) {
    let (caller) = req caller(@next);
    match {
        (@eos) => {
            become caller(@end, @none)
        }
        (@yield, item) => {
            let (caller) = req caller(@next);
            match {
                (@eos) => {
                    become caller(@end, @one, item)
                }
                (@yield, item2) => {
                    become caller(@end, @many, item, item2)
                }
            }
        }
    }
}

mod tests {
    proc enumerate(caller, @next) {
        let (caller, @next) = caller(@yield, @split_empty);
        let (caller, @next) = caller(@yield, @symbol_len);
        let (caller, @next) = caller(@yield, @symbol_iter);
        let (caller, @next) = caller(@yield, @peeking_empty);
        let (caller, @next) = caller(@yield, @peeking_some);
        let (caller, @next) = caller(@yield, @alternate);
        let (caller, @next) = caller(@yield, @merge_sorted);
        let (caller, @next) = caller(@yield, @mergesort_empty);
        let (caller, @next) = caller(@yield, @mergesort_singleton);
        let (caller, @next) = caller(@yield, @mergesort_two);
        let (caller, @next) = caller(@yield, @mergesort_full);
        // let (caller, @next) = caller(@yield, @parseint);
        let () = caller(@eos);        
    }

    mod cases {
        proc symbol_len(caller) {
            let (3) = this.super.super.builtin.symbol_len(@foo);
            let () = caller(@pass);
        }

        proc symbol_iter(caller) {
            let (iter) = this.super.super.symbol_chars(@foo);
            let (@resp, iter, @yield, 'f') = iter(@next);
            let (@resp, iter, @yield, 'o') = iter(@next);
            let (@resp, iter, @yield, 'o') = iter(@next);
            let (@end, @eos) = iter(@next);
            let () = caller(@pass);
        }

        proc peeking_empty(caller) {
            let (i) = this.super.super.empty();
            let (p) = this.super.super.peeking();
            let (p) = this.super.super.builtin.compose(i, p);

            let (@resp, p, @none) = p(@peek);
            let (@resp, p, @none) = p(@peek);
            let (@end, @eos) = p(@next);
            let () = caller(@pass);
        }

        proc peeking_some(caller) {
            let (i) = this.super.super.symbol_chars(@fox);
            let (p) = this.super.super.peeking();
            let (p) = this.super.super.builtin.compose(i, p);

            let (@resp, p, @some, 'f') = p(@peek);
            let (@resp, p, @some, 'f') = p(@peek);            
            let (@resp, p, @yield, 'f') = p(@next);            
            let (@resp, p, @some, 'o') = p(@peek);
            let (@resp, p, @some, 'o') = p(@peek);            
            let (@resp, p, @yield, 'o') = p(@next);            
            let (@resp, p, @some, 'x') = p(@peek);
            let (@resp, p, @some, 'x') = p(@peek);            
            let (@resp, p, @yield, 'x') = p(@next);            
            let (@resp, p, @none) = p(@peek);
            let (@resp, p, @none) = p(@peek);
            let (@end, @eos) = p(@next);
            let () = caller(@pass);
        }

        proc queue(caller) {
            let (q) = this.super.super.queue_empty;
            let (@resp, q) = q(@push_back, @foo);
            let (@resp, q) = q(@push_back, @bar);
            let (@resp, q) = q(@push_back, @baz);
            let (@resp, q, @yield, @foo) = q(@next);
            let (@resp, q, @yield, @bar) = q(@next);
            let (@resp, q, @yield, @baz) = q(@next);
            let (@resp, q, @eos) = q(@next);
            let () = caller(@pass);
        }

        proc stack(caller) {
            let (q) = this.super.super.stack_empty;
            let (q) = q(@push_front, @foo);
            let (q) = q(@push_front, @bar);
            let (q) = q(@push_front, @baz);
            let (q, @yield, @baz) = q(@next);
            let (q, @yield, @bar) = q(@next);
            let (q, @yield, @foo) = q(@next);
            let (q, @eos) = q(@next);
            let () = caller(@pass);
        }

        proc alternate(caller) {
            let (s) = this.super.super.alternate();
            let (@req, s, @next) = s(@get);
            let (@req, s, @next) = s(@yield, 'f');
            let (@req, s, @next) = s(@yield, 'o');
            let (@req, s, @next) = s(@yield, 'o');
            let (@req, s, @next) = s(@yield, 'b');
            let (@req, s, @next) = s(@yield, 'a');
            let (@req, s, @next) = s(@yield, 'r');
            let (@end, aq, bq) = s(@eos);
            let (@end, aq) = aq(@into_iter);
            let (@end, bq) = bq(@into_iter);
    
            let (@resp, aq, @yield, 'f') = aq(@next);
            let (@resp, aq, @yield, 'o') = aq(@next);
            let (@resp, aq, @yield, 'a') = aq(@next);
            let (@end, @eos) = aq(@next);
            let (@resp, bq, @yield, 'o') = bq(@next);
            let (@resp, bq, @yield, 'b') = bq(@next);
            let (@resp, bq, @yield, 'r') = bq(@next);
            let (@end, @eos) = bq(@next);

            let () = caller(@pass);
        }

        proc split_empty(caller) {
            let (s) = this.super.super.split;
            let (@resp, s, @start) = s(@next);
            let (@req, s, @next) = s(@req, @next);
            let (@resp, s, @eos) = s(@eos);
            let (@end, @eos) = s(@next);
            let () = caller(@pass);
        }

        proc split_full(caller) {
            let (s) = this.super.super.split;
            let (@resp, s, @start) = s(@next);
            let (@req, s, @next) = s(@req, @next);
            let (@resp, s, @start) = s(@non_delimiter);
            let (@req, s, @req, @foo) = s(@req, @req, @foo);
            let (@resp, s, @bar) = s(@bar);
            let (@req, s, @next) = s(@req, @next);
            let (@resp, s, @eos) = s(@delimiter);
            let (@resp, s, @start) = s(@next);
            let (@req, s, @next) = s(@req, @next);
            let (@resp, s, @start) = s(@non_delimiter);
            let (@req, s, @req, @baz) = s(@req, @req, @baz);
            let (@resp, s, @qux) = s(@qux);
            let (@req, s, @next) = s(@req, @next);
            let (@resp, s, @eos) = s(@eos);
            let (@end, @eos) = s(@next);

            let () = caller(@pass);
        }

        proc str_line_delimiter(caller, @next) {
            let (caller) = req caller(@next);
            match {
                (@eos) => {
                    become caller(@end, @eos)
                }
                (@start) => {
                    let (caller) = req caller(@req, @copy);
                    match {
                        ('\n') => {
                            let () = resp caller(@delimiter);
                            become this.str_line_delimiter()
                        }
                        (char) => {
                            let () = resp caller(@non_delimiter);
                            let () = this.thingy_proxy();
                            become this.str_line_delimiter()
                        }
                    }
                }
            }
        }
        
        proc thingy_proxy(next, caller) {
            match {
                (@req) => {
                    let (caller) = req caller();
                    let () = resp caller();
                    become this.thingy_proxy()
                }
                (other) => {
                    become next(caller, other)
                }
            }
        }

        proc merge_sorted(caller) {
            let (a) = this.super.super.list.new();
            let (@resp, a) = a(@push_back, 1);
            let (@resp, a) = a(@push_back, 3);
            let (@resp, a) = a(@push_back, 5);
            let (@resp, a) = a(@push_back, 6);
            let (@end, a) = a(@into_iter);
            let (b) = this.super.super.list.new();
            let (@resp, b) = b(@push_back, 2);
            let (@resp, b) = b(@push_back, 4);
            let (@resp, b) = b(@push_back, 6);
            let (@resp, b) = b(@push_back, 7);
            let (@resp, b) = b(@push_back, 8);
            let (@end, b) = b(@into_iter);

            let (c) = this.super.super.builtin.mk_pair(a, b);

            let (m) = this.super.super.builtin.compose(c, this.super.super.merge_sorted);
            let (@resp, m, @yield, 1) = m(@next);
            let (@resp, m, @yield, 2) = m(@next);
            let (@resp, m, @yield, 3) = m(@next);
            let (@resp, m, @yield, 4) = m(@next);
            let (@resp, m, @yield, 5) = m(@next);
            let (@resp, m, @yield, 6) = m(@next);
            let (@resp, m, @yield, 6) = m(@next);
            let (@resp, m, @yield, 7) = m(@next);
            let (@resp, m, @yield, 8) = m(@next);
            let (@resp, m, @eos) = m(@next);

            let () = caller(@pass);
        }

        proc get_test_iter(caller) {
            let (a) = this.super.super.list.new();
            let (@resp, a) = a(@push_back, 0); 
            let (@resp, a) = a(@push_back, 4); 
            let (@resp, a) = a(@push_back, 2); 
            let (@resp, a) = a(@push_back, 9); 
            let (@resp, a) = a(@push_back, 7); 
            let (@resp, a) = a(@push_back, 8); 
            let (@resp, a) = a(@push_back, 6); 
            let (@resp, a) = a(@push_back, 5); 
            let (@resp, a) = a(@push_back, 3); 
            let (@resp, a) = a(@push_back, 1);
            let (@end, a) = a(@into_iter);
            become caller(a)
        }

        proc mergesort_empty(caller) {
            let (empty) = this.super.super.empty();
            let (a) = this.super.super.builtin.compose(empty, this.super.super.mergesort);
            let (@end, @eos) = a(@next);

            let () = caller(@pass);
        }

        proc mergesort_singleton(caller) {
            let (q) = this.super.super.once(@foo);

            let (a) = this.super.super.builtin.compose(q, this.super.super.mergesort);
            let (@resp, a, @yield, @foo) = a(@next);
            let (@end, @eos) = a(@next);

            let () = caller(@pass);
        }
        
        proc mergesort_two(caller) {
            let (q) = this.super.super.list.new();
            let (@resp, q) = q(@push_back, 2);
            let (@resp, q) = q(@push_back, 1);
            let (@end, q) = q(@into_iter);
          
            let (a) = this.super.super.builtin.compose(q, this.super.super.mergesort);
            let (@resp, a, @yield, 1) = a(@next);
            let (@resp, a, @yield, 2) = a(@next);
            let (@resp, a, @eos) = a(@next);

            let () = caller(@pass);
        }

        proc mergesort_full(caller) {
            let (q) = this.get_test_iter();
          
            let (a) = this.super.super.builtin.compose(q, this.super.super.mergesort);

            let (@resp, a, @yield, 0) = a(@next); 
            let (@resp, a, @yield, 1) = a(@next);
            let (@resp, a, @yield, 2) = a(@next); 
            let (@resp, a, @yield, 3) = a(@next); 
            let (@resp, a, @yield, 4) = a(@next); 
            let (@resp, a, @yield, 5) = a(@next); 
            let (@resp, a, @yield, 6) = a(@next); 
            let (@resp, a, @yield, 7) = a(@next); 
            let (@resp, a, @yield, 8) = a(@next); 
            let (@resp, a, @yield, 9) = a(@next); 
            let (@resp, a, @eos) = a(@next);

            let () = caller(@pass);
        }

        proc parseint(caller) {
            let (parseint) = this.super.super.parseint();
            let (parseint) = parseint(@push, '6');
            let (parseint) = parseint(@push, '7');
            let (parseint) = parseint(@push, '2');
            let (@ok, 672) = parseint(@finish);

            let () = caller(@pass);
        }
    }

    proc run(caller, case_name) {
        R{ @cases this #get #get @exec }
    }
}
