
proc new(caller) {
    become caller(this.list_empty)
}

proc list_empty(caller) {
    match {
        (@push_front, item) => {
            let (caller) = resp caller();
            become this.list_cons(item, this.list_empty, caller)
        }
        (@push_back, item) => {
            let (caller) = resp caller();
            become this.list_cons(item, this.list_empty, caller)
        }
        (@pop_front) => {
            let (caller) = resp caller(@none);
            become this.list_empty(caller)
        }
        (@into_iter) => {
            let (empty) = this.new();
            become caller(@end, this.list_into_iter&(empty))
        }
        (@is_empty) => {
            let (caller) = resp caller(true);
            become this.list_empty(caller)
        }
    }
}

proc list_cons(head, tail, caller) {
    match {
        (@push_front, item) => {
            let (caller) = resp caller();
            let (tail) = this.list_cons&(head, tail);
            become this.list_cons(item, tail, caller)
        }
        (@push_back, item) => {
            let (@resp, tail) = tail(@push_back, item);
            let (caller) = resp caller();
            become this.list_cons(head, tail, caller)
        }
        (@pop_front) => {
            let (caller) = resp caller(@some, head);
            become tail(caller)
        }
        (@into_iter) => {
            let (self) = this.list_cons&(head, tail);
            become caller(@end, this.list_into_iter&(self))
        }
        (@is_empty) => {
            let (caller) = resp caller(false);
            become this.list_cons(head, tail, caller)
        }
    }
}

proc list_into_iter(list, caller, @next) {
    let (@resp, list) = list(@pop_front);
    match {
        (@none) => {
            become caller(@end, @eos)
        }
        (@some, item) => {
            let (caller) = resp caller(@yield, item);
            become this.list_into_iter(list, caller)
        }
    }
}

mod tests {
    proc enumerate(caller, @next) {
        let (caller, @next) = caller(@yield, @basic);
        let (caller, @next) = caller(@yield, @iter);
        let () = caller(@eos);        
    }

    mod cases {
        proc basic(caller) {
            let (q) = this.super.super.new();
            let (@resp, q) = q(@push_back, @foo);
            let (@resp, q) = q(@push_back, @bar);
            let (@resp, q) = q(@push_back, @baz);
            let (@resp, q, @some, @foo) = q(@pop_front);
            let (@resp, q, @some, @bar) = q(@pop_front);
            let (@resp, q, @some, @baz) = q(@pop_front);
            let (@resp, q, @none) = q(@pop_front);
            let () = caller(@pass);
        }

        proc iter(caller) {
            let (q) = this.super.super.new();
            let (@resp, q) = q(@push_back, @foo);
            let (@resp, q) = q(@push_back, @bar);
            let (@resp, q) = q(@push_back, @baz);
            let (@end, i) = q(@into_iter);
            let (@resp, i, @yield, @foo) = i(@next);
            let (@resp, i, @yield, @bar) = i(@next);
            let (@resp, i, @yield, @baz) = i(@next);
            let (@end, @eos) = i(@next);
            let () = caller(@pass);
        }
    }

    proc run(caller, case_name) {
        R{ @cases this #get #get @exec }
    }
}
